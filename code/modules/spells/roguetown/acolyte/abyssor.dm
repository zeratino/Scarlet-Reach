//t1, the bends
/obj/effect/proc_holder/spell/invoked/abyssor_bends
	name = "Depth Bends"
	overlay_state = "thebends"
	releasedrain = 15
	chargedrain = 0
	chargetime = 2 SECONDS
	range = 15
	movement_interrupt = FALSE
	chargedloop = null
	sound = 'sound/foley/bubb (5).ogg'
	invocation = "Weight of the deep, crush!"
	invocation_type = "shout"
	associated_skill = /datum/skill/magic/holy
	antimagic_allowed = TRUE
	recharge_time = 20 SECONDS
	miracle = TRUE
	devotion_cost = 15
	var/base_fatdrain = 10

/obj/effect/proc_holder/spell/invoked/abyssor_bends/cast(list/targets, mob/user = usr)
	. = ..()
	if(isliving(targets[1]))
		var/mob/living/target = targets[1]
		user.visible_message("<font color='yellow'>[user] makes a fist at [target]!</font>")
		if(istype(target, /mob/living/carbon))
			var/mob/living/carbon = target
			if(carbon.patron?.type != /datum/patron/divine/abyssor)
				var/fatdrain = user.get_skill_level(associated_skill) * base_fatdrain
				carbon.stamina_add(fatdrain)
		target.Dizzy(10)
		target.blur_eyes(20)
		target.emote("drown")
		return TRUE
	revert_cast()
	return FALSE

//T0. Stands the character up, if they can stand.
/obj/effect/proc_holder/spell/self/abyssor_wind
	name = "Second Wind"
	desc = "Rise if fallen, and regain some of your stamina."
	overlay_state = "abyssor_wind"
	releasedrain = 10
	chargedrain = 0
	chargetime = 0
	sound = 'sound/magic/abyssor_splash.ogg'
	associated_skill = /datum/skill/magic/holy
	antimagic_allowed = FALSE
	invocation = "What is drowned shall rise anew!"
	invocation_type = "shout"
	recharge_time = 120 SECONDS
	devotion_cost = 30
	miracle = TRUE
	var/stamregenmod = 5	//How many % of stamina we regain after cast, scales with holy skill.

/obj/effect/proc_holder/spell/self/abyssor_wind/cast(list/targets, mob/user)
	if(!ishuman(user))
		revert_cast()
		return FALSE
	var/mob/living/carbon/human/H = user
	if(H.IsStun() || H.IsImmobilized() || H.IsOffBalanced())
		to_chat(user, span_warning("I am too incapacitated!"))
		revert_cast()
		return FALSE
	var/msg = span_warning("[user] ")
	if(H.resting)
		H.set_resting(FALSE, FALSE)
		msg += span_warning("rises and ")
	var/regen = (stamregenmod / 100) * H.get_skill_level(associated_skill)
	H.stamina_add(-(regen * H.max_stamina))
	H.energy_add(regen * H.max_energy)
	msg += span_warning("becomes invigorated!")
	H.visible_message(msg)
	return TRUE

//T0 The Fishing
/obj/effect/proc_holder/spell/invoked/aquatic_compulsion
	name = "Aquatic Compulsion"
	overlay_state = "aqua"
	releasedrain = 15
	chargedrain = 0
	chargetime = 0.5 SECONDS
	range = 3
	movement_interrupt = FALSE
	chargedloop = null
	sound = 'sound/foley/bubb (5).ogg'
	invocation = "Splash forth."
	invocation_type = "shout"
	associated_skill = /datum/skill/magic/holy
	antimagic_allowed = TRUE
	recharge_time = 10 SECONDS
	miracle = TRUE
	devotion_cost = 10
	//Horrendous carry-over from fishing code
	var/frwt = list(/turf/open/water/river, /turf/open/water/cleanshallow, /turf/open/water/pond)
	var/salwt = list(/turf/open/water/ocean, /turf/open/water/ocean/deep)
	var/list/freshfishloot = list(
		/obj/item/reagent_containers/food/snacks/fish/carp = 225,
		/obj/item/reagent_containers/food/snacks/fish/sunny = 325,
		/obj/item/reagent_containers/food/snacks/fish/salmon = 190,
		/obj/item/reagent_containers/food/snacks/fish/eel = 140,
		/obj/item/reagent_containers/food/snacks/smallrat = 1, //funny
		/mob/living/simple_animal/hostile/retaliate/rogue/mudcrab = 20,			
	)
	var/list/seafishloot = list(
		/obj/item/reagent_containers/food/snacks/fish/cod = 190,
		/obj/item/reagent_containers/food/snacks/fish/plaice = 210,
		/obj/item/reagent_containers/food/snacks/fish/sole = 340,
		/obj/item/reagent_containers/food/snacks/fish/angler = 140,
		/obj/item/reagent_containers/food/snacks/fish/lobster = 150,
		/obj/item/reagent_containers/food/snacks/fish/bass = 210,
		/obj/item/reagent_containers/food/snacks/fish/clam = 40,
		/obj/item/reagent_containers/food/snacks/fish/clownfish = 20,
		/obj/item/reagent_containers/food/snacks/smallrat = 1, //still funny
		/mob/living/carbon/human/species/goblin/npc/sea = 10,
		/mob/living/simple_animal/hostile/rogue/deepone = 3,
		/mob/living/simple_animal/hostile/rogue/deepone/spit = 3,			
	)

/obj/effect/proc_holder/spell/invoked/aquatic_compulsion/cast(list/targets, mob/user = usr)
	. = ..()
	if(isturf(targets[1]))
		var/turf/T = targets[1]
		var/success
		var/A
		if(T.type in frwt)
			A = pickweight(freshfishloot)
			success = TRUE
		if(T.type in salwt)
			A = pickweight(seafishloot)
			success = TRUE
		if(success)
			var/atom/movable/AF = new A(T)
			AF.throw_at(get_turf(user), 5, 1, null)
			record_featured_stat(FEATURED_STATS_FISHERS, user)
			GLOB.azure_round_stats[STATS_FISH_CAUGHT]++
			playsound(T, 'sound/foley/footsteps/FTWAT_1.ogg', 100)
			user.visible_message("<font color='yellow'>[user] makes a beckoning gesture at [T]!</font>")
			return TRUE
		else
			revert_cast()
			return FALSE
	revert_cast()
	return FALSE

//T2, Abyssal Healing. Totally stole most of this from lesser heal.
/obj/effect/proc_holder/spell/invoked/abyssheal
	name = "Abyssal Healing"
	overlay_state = "thebends"
	releasedrain = 15
	chargedrain = 0
	chargetime = 1 SECONDS
	range = 2
	warnie = "sydwarning"
	movement_interrupt = FALSE
	sound = 'sound/foley/waterenter.ogg'
	invocation = "Healing waters, come forth!"
	invocation_type = "shout"
	associated_skill = /datum/skill/magic/holy
	antimagic_allowed = TRUE
	recharge_time = 10 SECONDS
	miracle = TRUE
	devotion_cost = 50

/obj/effect/proc_holder/spell/invoked/abyssheal/cast(list/targets, mob/living/user)
	. = ..()
	if(isliving(targets[1]))
		var/mob/living/target = targets[1]
		if(HAS_TRAIT(target, TRAIT_PSYDONITE))
			target.visible_message(span_info("[target] stirs for a moment, the miracle dissipates."), span_notice("A dull warmth swells in your heart, only to fade as quickly as it arrived."))
			playsound(target, 'sound/magic/PSY.ogg', 100, FALSE, -1)
			user.playsound_local(user, 'sound/magic/PSY.ogg', 100, FALSE, -1)
			return FALSE
		if(user.patron?.undead_hater && (target.mob_biotypes & MOB_UNDEAD)) //THE DEEP CALLS- sorry, the pressure of the deep falls upon those of the undead ilk
			target.visible_message(span_danger("[target] is crushed by divine pressure!"), span_userdanger("I'm crushed by divine pressure!"))
			target.adjustBruteLoss(30)			
			return TRUE
		var/conditional_buff = FALSE
		var/situational_bonus = 1
		target.visible_message(span_info("A wave of divine energy crashes over [target]!"), span_notice("I'm crushed by healing energies!"))
		var/list/water = list(/turf/open/water/bath, /turf/open/water/ocean, /turf/open/water/cleanshallow, /turf/open/water/swamp, /turf/open/water/swamp/deep, /turf/open/water/pond, /turf/open/water/river)
		situational_bonus = 0
		// the more warter around us, the more we heal
		for (var/turf/O in oview(3, user))
			if (O in water)
				situational_bonus = min(situational_bonus + 0.1, 2)
		for (var/turf/open/water/ocean/deep/O in oview(3, user))
			situational_bonus += 0.5
		// Healing by the deep sea gives an extra boost.
		if (situational_bonus > 0)
			conditional_buff = TRUE
		var/healing = 6.5
		target.adjustFireLoss(-80)
		if (conditional_buff)
			to_chat(user, "Calling upon Abyssor's power is easier in these conditions!")
			healing += situational_bonus
			target.adjustFireLoss(-40)
		target.apply_status_effect(/datum/status_effect/buff/healing, healing)
		return TRUE

	revert_cast()
	return FALSE
//t3 alt, land surf, i just removed it but if this idea is like better... we'll see

//t3, possible t4 if I put in land surf, summon mossback
/obj/effect/proc_holder/spell/invoked/call_mossback
	name = "Call Mossback"
	overlay_state = "thebends"
	range = 7
	no_early_release = TRUE
	charging_slowdown = 1
	releasedrain = 20
	chargedrain = 0
	chargetime = 2 SECONDS
	chargedloop = null
	sound = 'sound/foley/bubb (1).ogg'
	invocation = "From the abyss, rise!"
	invocation_type = "shout"
	associated_skill = /datum/skill/magic/holy
	antimagic_allowed = TRUE
	recharge_time = 10 SECONDS
	miracle = TRUE
	devotion_cost = 100
	var/townercrab = TRUE //I was looking at this for three days and i am utterly stupid for not fixing it
	var/mob/living/simple_animal/hostile/retaliate/rogue/mossback/summoned

/obj/effect/proc_holder/spell/invoked/call_mossback/cast(list/targets, mob/living/user)
	. = ..()
	var/turf/T = get_turf(targets[1])
	if(isopenturf(T))
		if(!user.mind.has_spell(/obj/effect/proc_holder/spell/invoked/minion_order))
			user.mind.AddSpell(new /obj/effect/proc_holder/spell/invoked/minion_order)
		QDEL_NULL(summoned)
		summoned = new /mob/living/simple_animal/hostile/retaliate/rogue/mossback(T, user, townercrab)
		return TRUE
	else
		to_chat(user, span_warning("The targeted location is blocked. My call fails to draw a mossback."))
		return FALSE

/obj/effect/proc_holder/spell/invoked/call_dreamfiend
	name = "Summon Dreamfiend"
	overlay_state = "dreamfiend"
	range = 7
	no_early_release = TRUE
	charging_slowdown = 1
	chargetime = 2 SECONDS
	sound = 'sound/foley/bubb (1).ogg'
	invocation = "From the dream, consume!"
	invocation_type = "shout"
	recharge_time = 300 SECONDS
	miracle = TRUE
	devotion_cost = 150

	// Teleport parameters
	var/inner_tele_radius = 1
	var/outer_tele_radius = 2
	var/include_dense = FALSE
	var/include_teleport_restricted = FALSE

/obj/effect/proc_holder/spell/invoked/call_dreamfiend/cast(list/targets, mob/living/user)
	. = ..()
	var/mob/living/carbon/target = targets[1]
	
	if(!istype(target))
		to_chat(user, span_warning("This spell only works on creatures capable of dreaming!"))
		revert_cast()
		return FALSE
	
	if(!summon_dreamfiend(
		target = target,
		user = user,
		F = /mob/living/simple_animal/hostile/rogue/dreamfiend,
		outer_tele_radius = outer_tele_radius,
		inner_tele_radius = inner_tele_radius,
		include_dense = FALSE,
		include_teleport_restricted = FALSE
	))
		to_chat(user, span_warning("No valid space to manifest the dreamfiend!"))
		revert_cast()
		return FALSE

	return TRUE

/proc/summon_dreamfiend(mob/living/target, mob/living/user, mob/F = /mob/living/simple_animal/hostile/rogue/dreamfiend, outer_tele_radius = 3, inner_tele_radius = 2, include_dense = FALSE, include_teleport_restricted = FALSE)
	var/turf/target_turf = get_turf(target)
	var/list/turfs = list()

	// Reused turf selection logic from blink_to_target
	for(var/turf/T in range(target_turf, outer_tele_radius))
		if(T in range(target_turf, inner_tele_radius))
			continue
		if(istransparentturf(T))
			continue
		if(T.density && !include_dense)
			continue
		if(T.teleport_restricted && !include_teleport_restricted)
			continue
		if(T.x>world.maxx-outer_tele_radius || T.x<outer_tele_radius)
			continue
		if(T.y>world.maxy-outer_tele_radius || T.y<outer_tele_radius)
			continue
		turfs += T

	if(!length(turfs))
		for(var/turf/T in orange(target_turf, outer_tele_radius))
			if(!(T in orange(target_turf, inner_tele_radius)))
				turfs += T

	if(!length(turfs))
		return FALSE

	var/turf/spawn_turf = pick(turfs)
	
	F = new F(spawn_turf)
	F.ai_controller.set_blackboard_key(BB_BASIC_MOB_CURRENT_TARGET, target)
	F.ai_controller.set_blackboard_key(BB_MAIN_TARGET, target)
	
	F.visible_message(span_notice("A [F] manifests following after [target]... countless teeth bared with hostility!"))
	return TRUE

/obj/effect/proc_holder/spell/invoked/abyssal_infusion
	name = "Abyssal Infusion"
	overlay_state = "abyssal_infusion"
	range = 7
	no_early_release = TRUE
	charging_slowdown = 1
	chargetime = 2 SECONDS
	sound = 'sound/foley/bubb (1).ogg'
	//Each dreamfiend has a different name to call!
	invocation = "shogg vulgt!"
	invocation_type = "shout"
	recharge_time = 600 SECONDS
	miracle = TRUE
	devotion_cost = 300

/obj/effect/proc_holder/spell/invoked/abyssal_infusion/cast(list/targets, mob/living/user)
	. = ..()
	var/mob/living/carbon/human/target = targets[1]

	if(!istype(target, /mob/living/carbon/human) || target.mind == null)
		to_chat(user, span_warning("This spell only works on creatures capable of dreaming!"))
		revert_cast()
		return FALSE

	if(target == user)
		to_chat(user, span_warning("You must maintain the connection to the dreamfiend from a safe spiritual distance or risk being consumed yourself!"))
		revert_cast()
		return FALSE

	if(target.mind.has_spell(/obj/effect/proc_holder/spell/invoked/abyssal_strength))
		to_chat(user, span_warning("[target] is already blessed with Abyssor's strength."))
		revert_cast()
		return FALSE

	var/anglerfish_found = FALSE
	var/list/held_items = list()

	for(var/obj/item/I in user.held_items)
		held_items += I

	for(var/obj/item/I in held_items)
		if(istype(I, /obj/item/reagent_containers/food/snacks/fish/angler))
			qdel(I)
			anglerfish_found = TRUE
			break

	if(!anglerfish_found)
		to_chat(user, span_warning("An anglerfish is required to channel the abyssal energies!"))
		revert_cast()
		return FALSE

	target.mind.AddSpell(new /obj/effect/proc_holder/spell/invoked/abyssal_strength)
	to_chat(target, span_warning("My mind writhes, revealing a new ability."))

	return TRUE

/obj/effect/proc_holder/spell/invoked/abyssal_strength
	name = "Abyssal Strength"
	overlay_state = "abyssal_strength1"
	range = 7
	no_early_release = TRUE
	charging_slowdown = 1
	chargetime = 2 SECONDS
	sound = 'sound/foley/bubb (1).ogg'
	//Each dreamfiend has a different name to call!
	invocation = "shogg vulgt!"
	invocation_type = "shout"
	recharge_time = 750 SECONDS

	var/stage = 1
	var/casts_in_stage = 0
	var/current_stage3_chance = 50
	var/static/list/stage_mobs = list(
		/mob/living/simple_animal/hostile/rogue/dreamfiend,
		/mob/living/simple_animal/hostile/rogue/dreamfiend/major,
		/mob/living/simple_animal/hostile/rogue/dreamfiend/ancient
	)

/obj/effect/proc_holder/spell/invoked/abyssal_strength/cast(list/targets, mob/living/user)
	. = ..()
	var/mob/living/carbon/target = targets[1]

	if(!istype(target) || !(target == user))
		to_chat(user, span_warning("This spell only works on myself!"))
		revert_cast()
		return FALSE

	var/list/stats = list(
		"str" = 3 + ((stage - 1) * 1),
		"con" = 1 + (stage * 2),
		"end" = 1 + (stage * 2),
		"fort" = 1 - (stage * 2),
		"speed" = 1 - (stage * 2),
		"per" = -1 * stage
	)

	var/summon_chance = 0
	var/spawn_type
	switch(stage)
		if(1)
			summon_chance = 5 + (casts_in_stage * 35)
			spawn_type = stage_mobs[1]
		if(2)
			summon_chance = 10 + (casts_in_stage * 40)
			spawn_type = stage_mobs[2]
		if(3)
			summon_chance = current_stage3_chance
			current_stage3_chance += rand(1,20)
			spawn_type = stage_mobs[3]

	if(prob(summon_chance))
		summon_dreamfiend(target = user, user = user, F = spawn_type)
		to_chat(user, span_userdanger("You feel the dream manifest in reality, bearing a horrifying form!"))
		user.mind.RemoveSpell(src)
		return

	if(stage < 3)
		casts_in_stage++
		if(casts_in_stage > 2)
			stage++
			casts_in_stage = 0
			if(stage == 3)
				to_chat(user, span_warning("I can feel countless slimy and oozing teeth biting into my skin! Something horrifying is observing me!"))
			else
				to_chat(user, span_warning("The whispers in your head grow louder..."))
	else
		casts_in_stage = min(casts_in_stage + 1, 100)
	
	target.apply_status_effect(
		/datum/status_effect/buff/abyssal,
		stats["str"],
		stats["con"],
		stats["end"],
		stats["fort"],
		stats["speed"],
		stats["per"]
	)

	overlay_state = "abyssal_strength[stage]"

	return TRUE

/atom/movable/screen/alert/status_effect/buff/abyssal
	name = "Abyssal strength"
	desc = "I feel an unnatural power dwelling in my limbs."
	icon_state = "abyssal"

#define ABYSSAL_FILTER "abyssal_glow"

/datum/status_effect/buff/abyssal
	var/dreamfiend_chance = 0
	var/stage = 1
	var/str_buff = 3
	var/con_buff = 3
	var/end_buff = 3
	var/speed_malus = 0
	var/fortune_malus = 0
	var/perception_malus = 0
	var/outline_colour ="#00051f"
	alert_type = /atom/movable/screen/alert/status_effect/buff/abyssal
	examine_text = "SUBJECTPRONOUN has muscles swollen with a strange pale strength."
	id = "abyssal_strength"
	duration = 450 SECONDS

/datum/status_effect/buff/abyssal/on_creation(mob/living/new_owner, new_str, new_con, new_end, new_fort, new_speed, new_per)
	str_buff = new_str
	con_buff = new_con
	end_buff = new_end
	fortune_malus = new_fort
	speed_malus = new_speed
	perception_malus = new_per

	effectedstats = list(
		"strength" = str_buff,
		"constitution" = con_buff,
		"endurance" = end_buff,
		"fortune" = fortune_malus,
		"speed" = speed_malus,
		"perception" = perception_malus
	)
	
	return ..()

/datum/status_effect/buff/abyssal/on_apply()
	. = ..()
	var/filter = owner.get_filter(ABYSSAL_FILTER)
	ADD_TRAIT(owner, TRAIT_STRENGTH_UNCAPPED, TRAIT_MIRACLE)
	if (!filter)
		owner.add_filter(ABYSSAL_FILTER, 2, list("type" = "outline", "color" = outline_colour, "alpha" = 180, "size" = 1))
	to_chat(owner, span_warning("My limbs swell with otherworldly power!"))

/datum/status_effect/buff/abyssal/on_remove()
	. = ..()
	REMOVE_TRAIT(owner, TRAIT_STRENGTH_UNCAPPED, TRAIT_MIRACLE)
	owner.remove_filter(ABYSSAL_FILTER)
	to_chat(owner, span_warning("the strange power fades"))

#undef ABYSSAL_FILTER
