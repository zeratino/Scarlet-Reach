// Lich / Vampire shared list only
/obj/effect/proc_holder/spell/invoked/projectile/bloodlightning
	name = "Blood Bolt"
	desc = "Emit a bolt of blood lightning that burns a target harshly, forcing them to drop items, preventing them from attacking, and slowing them down for a short time."
	clothes_req = FALSE
	overlay_state = "bloodlightning"
	sound = 'sound/magic/vlightning.ogg'
	range = 8
	projectile_type = /obj/projectile/magic/bloodlightning
	releasedrain = 30
	chargedrain = 1
	chargetime = 25
	recharge_time = 20 SECONDS
	warnie = "spellwarning"
	no_early_release = TRUE
	movement_interrupt = FALSE
	spell_tier = 2 // Doesn't matter for the most part
	invocation = "Sanguis Sagitta!"
	invocation_type = "shout"
	glow_color = GLOW_COLOR_VAMPIRIC
	glow_intensity = GLOW_INTENSITY_MEDIUM
	charging_slowdown = 3
	chargedloop = /datum/looping_sound/invokegen
	associated_skill = /datum/skill/magic/blood
	cost = 3

/obj/projectile/magic/bloodlightning
	name = "blood bolt"
	tracer_type = /obj/effect/projectile/tracer/blood
	muzzle_type = null
	impact_type = null
	hitscan = TRUE
	movement_type = UNSTOPPABLE
	damage = 80
	npc_damage_mult = 2
	damage_type = BURN
	nodamage = FALSE
	speed = 0.3
	flag = "magic"
	light_color = "#802121"
	light_outer_range = 7

/obj/projectile/magic/bloodlightning/on_hit(target)
	. = ..()
	if(ismob(target))
		var/mob/M = target
		if(M.anti_magic_check())
			visible_message(span_warning("[src] fizzles on contact with [target]!"))
			playsound(target, 'sound/magic/magic_nulled.ogg', 100)
			qdel(src)
			return BULLET_ACT_BLOCK
		if(isliving(target))
			var/mob/living/L = target
			L.Immobilize(1 SECONDS)//much longer immobilize than standard bolt since it's an antagonist spell and they'd likely need it much more
			L.apply_status_effect(/datum/status_effect/debuff/clickcd, 3 SECONDS)
			L.electrocute_act(1, src, 1, SHOCK_NOSTUN)
			for(var/obj/item/W in L.held_items)
				L.dropItemToGround(W)
			L.apply_status_effect(/datum/status_effect/buff/lightningstruck, 1.5 SECONDS)
	qdel(src)
